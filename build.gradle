plugins {
    id 'java'
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "org.owasp.dependencycheck" version "5.3.1"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

project.ext.releaseMode = project.hasProperty("releaseMode")

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'github-release-action')

project.ext.publishRepo = "https://maven.pkg.github.com/reportportal/commons-rules"

apply from: scriptsUrl + '/release-commons.gradle'
apply from: scriptsUrl + '/release-github.gradle'
apply from: scriptsUrl + '/build-quality.gradle'


repositories {
    mavenCentral()
    releaseMode ? maven { url "http://dl.bintray.com/epam/reportportal" } : maven { url 'https://jitpack.io' }
}


dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.5.RELEASE'
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.github.reportportal:commons-bom:7affe20')
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-model'
    } else {
        compile 'com.github.reportportal:commons-model:22ccaac'
    }

    compile 'org.slf4j:slf4j-api'
    compile 'com.google.guava:guava'
    compile 'org.springframework:spring-webmvc'
    compile 'org.springframework.security:spring-security-core'
    implementation 'javax.servlet:javax.servlet-api'


    testImplementation 'junit:junit'
    testImplementation 'org.hamcrest:hamcrest-all'
    testImplementation 'javax.servlet:javax.servlet-api'
}

wrapper {
    gradleVersion = '6.0'
}

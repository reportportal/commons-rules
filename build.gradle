plugins {
    id 'java'
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "org.owasp.dependencycheck" version "5.3.1"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    publishRepo = "https://maven.pkg.github.com/reportportal/commons-rules"
    dependencyRepos = ["commons-model", "commons-bom"]
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: scriptsUrl + '/release-commons.gradle'
apply from: scriptsUrl + '/signing.gradle'
apply from: scriptsUrl + '/build-quality.gradle'


repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}


dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.5.RELEASE'
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.github.reportportal:commons-bom:211b2052bd')
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-model'
    } else {
        compile 'com.github.reportportal:commons-model:83f012f'
    }

    compile 'org.slf4j:slf4j-api'
    compile 'com.google.guava:guava'
    compile 'org.springframework:spring-webmvc'
    compile 'org.springframework.security:spring-security-core'
    implementation 'javax.servlet:javax.servlet-api'


    testImplementation 'junit:junit'
    testImplementation 'org.hamcrest:hamcrest-all'
    testImplementation 'javax.servlet:javax.servlet-api'
}

wrapper {
    gradleVersion = '6.0'
}
